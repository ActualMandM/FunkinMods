import Std;
import flixel.FlxG;
import funkin.FunkinMemory;
import funkin.Paths;
import funkin.audio.FunkinSound;
import funkin.modding.events.ScriptEvent;
import funkin.modding.module.Module;
import funkin.play.PlayState;
import funkin.save.Save;
import funkin.ui.options.OptionsState;

/**
 * A module that plays a sound whenever a note is hit.
 *
 * @author RagdollClash
 */
class SekaiHitSoundModule extends Module
{
  var HITSOUNDS:Dynamic = [
    "perfect" => "perfect",
    "killer" => "perfect", // https://youtu.be/BSi88nRijoA
    "sick" => "perfect",
    "good" => "great",
    "bad" => "good",
    "shit" => "good"
  ];

  var save:FlxSave = null;
  var folder:String = "sekai01";
  var volume:Float = 0.8;

  var options:Dynamic = {
    folder: "Sekai #1",
    volume: 80
  };

  var folderNames:Dynamic = [
    "Sekai #1" => "sekai01",
    "Sekai #2" => "sekai02"
  ];

  function new():Void
  {
    super("SekaiHitSoundModule");

    if (Save.instance.modOptions.get("mandm.sekaihitsound") == null)
    {
      Save.instance.modOptions.set("mandm.sekaihitsound", options);
      Save.instance.flush();
    }
    else
    {
      options = Save.instance.modOptions.get("mandm.sekaihitsound");
    }

    if (options.folder == null)
    {
      options.folder = "Sekai #1";
    }

    if (options.volume == null)
    {
      options.volume = 80;
    }

    folder = folderNames.get(options.folder);
    volume = options.volume / 100;
  }

  function onStateChangeEnd(event):Void
  {
    var isPlayState:Bool = Std.isOfType(event.targetState, PlayState);
    var isOptions:Bool = Std.isOfType(event.targetState, OptionsState);

    if (isOptions)
    {
      var prefs = event.targetState.optionsCodex.pages.get("preferences");

      prefs.createPrefItemEnum("Hitsound Type", "Change what the hitsound sounds like.", folderNames, function(value)
      {
        options.folder = value;
        folder = folderNames.get(value);
      }, options.folder);

      prefs.createPrefItemPercentage("Hitsound Volume", "Change the loudness of the hitsound.", function(value)
      {
        options.volume = value;
        volume = value / 100;
      }, options.volume);
    }
    else
    {
      Save.instance.modOptions.set("mandm.sekaihitsound", options);
      Save.instance.flush();

      if (isPlayState)
      {
        for (audio in HITSOUNDS)
        {
          FunkinMemory.cacheSound(Paths.sound(getHitSoundPath(audio)));
        }
      }
    }

    super.onStateChangeEnd(event);
  }

  override function onNoteHit(event:HitNoteScriptEvent):Void
  {
    super.onNoteHit(event);

    if (PlayState.instance == null || !event.note.noteData.getMustHitNote() || !HITSOUNDS.exists(event.judgement))
    {
      return;
    }

    FunkinSound.playOnce(Paths.sound(getHitSoundPath(HITSOUNDS.get(event.judgement))), volume);
  }

  function getHitSoundPath(type:String = "perfect"):String
  {
    return 'hitsound/' + folder + '/' + type;
  }
}
