import Std;
import flixel.FlxG;
import funkin.modding.events.ScriptEvent;
import funkin.modding.module.Module;
import funkin.play.PlayState;
import funkin.play.scoring.Scoring;
import funkin.save.Save;
import funkin.ui.options.OptionsState;

/**
 * A module that changes various values to recreate the
 * Playback Rate feature that is present in various FNF engines.
 *
 * @author RagdollClash
 */
class PlaybackRateModule extends Module
{
  var timeScale:Float = 1.0;
  var adjustScroll:Bool = true;

  var options:Dynamic = {
    timeScale: 100,
    adjustScroll: true
  };

  function new():Void
  {
    super("PlaybackRateModule");

    if (Save.instance.modOptions.get("mandm.playbackrate") == null)
    {
      Save.instance.modOptions.set("mandm.playbackrate", options);
      Save.instance.flush();
    }
    else
    {
      options = Save.instance.modOptions.get("mandm.playbackrate");
    }

    if (options.timeScale == null)
    {
      options.timeScale = 100;
    }

    if (options.adjustScroll == null)
    {
      options.adjustScroll = true;
    }

    timeScale = options.timeScale / 100;
    adjustScroll = options.adjustScroll;
  }

  function adjustTimeScale(scale:Float):Void
  {
    FlxG.timeScale = scale;

    // PBOT1 (Points Based On Timing v1)
    Scoring.PBOT1_PERFECT_THRESHOLD = 5.0 * scale;
    Scoring.PBOT1_MISS_THRESHOLD = 160.0 * scale;
    Scoring.PBOT1_KILLER_THRESHOLD = 12.5 * scale;
    Scoring.PBOT1_SICK_THRESHOLD = 45.0 * scale;
    Scoring.PBOT1_GOOD_THRESHOLD = 90.0 * scale;
    Scoring.PBOT1_BAD_THRESHOLD = 135.0 * scale;
    Scoring.PBOT1_SHIT_THRESHOLD = 160.0 * scale;

    // Legacy
    Scoring.LEGACY_HIT_WINDOW = 166.66666666666666 * scale;
    Scoring.LEGACY_SICK_THRESHOLD = 0.2 * scale;
    Scoring.LEGACY_GOOD_THRESHOLD = 0.75 * scale;
    Scoring.LEGACY_BAD_THRESHOLD = 0.9 * scale;
    Scoring.LEGACY_SHIT_THRESHOLD = 1.0 * scale;

    // Week 7
    Scoring.WEEK7_HIT_WINDOW = Scoring.LEGACY_HIT_WINDOW;
    Scoring.WEEK7_BAD_THRESHOLD = 0.8 * scale;
    Scoring.WEEK7_GOOD_THRESHOLD = 0.55 * scale;
    Scoring.WEEK7_SICK_THRESHOLD = 0.2 * scale;

    if (PlayState.instance != null)
    {
      PlayState.instance.playbackRate = scale;
    }
  }

  override function onCreate(event:ScriptEvent):Void
  {
    adjustTimeScale(1.0);
    super.onCreate(event);
  }

  override function onStateChangeEnd(event:StateChangeScriptEvent):Void
  {
    var isPlayState:Bool = Std.isOfType(event.targetState, PlayState);
    var isOptions:Bool = Std.isOfType(event.targetState, OptionsState);

    adjustTimeScale(isPlayState ? timeScale : 1.0);

    if (isOptions)
    {
      var prefs = event.targetState.optionsCodex.pages.get("preferences");

      prefs.createPrefItemPercentage("Playback Rate", "Changes how fast or slow the song plays at.", function(value)
      {
        options.timeScale = value;
        timeScale = value / 100;
      }, options.timeScale, 50, 300);

      prefs.createPrefItemCheckbox("Adjust Scroll Speed", "If enabled, the note spacing will be adjusted according to the song speed.", function(value)
      {
        options.adjustScroll = value;
        adjustScroll = value;
      }, options.adjustScroll);
    }
    else
    {
      Save.instance.modOptions.set("mandm.playbackrate", options);
      Save.instance.flush();
    }

    super.onStateChangeEnd(event);
  }

  override function onSongLoaded(event:SongLoadScriptEvent):Void
  {
    adjustTimeScale(timeScale);

    if (adjustScroll)
    {
      PlayState.instance.currentChart.scrollSpeed /= timeScale;
    }

    super.onSongLoaded(event);
  }

  override function onPause(event:PauseScriptEvent):Void
  {
    adjustTimeScale(1.0);
    super.onPause(event);
  }

  override function onResume(event:ScriptEvent):Void
  {
    adjustTimeScale(timeScale);
    super.onResume(event);
  }

  override function onSongEnd(event:ScriptEvent):Void
  {
    adjustTimeScale(1.0);
    super.onSongEnd(event);
  }

  override function onGameOver(event:ScriptEvent):Void
  {
    adjustTimeScale(1.0);
    super.onGameOver(event);
  }

  override function onSongRetry(event:ScriptEvent):Void
  {
    if (adjustScroll)
    {
      PlayState.instance.currentChart.scrollSpeed *= timeScale;
    }

    super.onSongRetry(event);
  }

  override function onDestroy(event:ScriptEvent):Void
  {
    adjustTimeScale(1.0);

    if (adjustScroll && PlayState.instance != null)
    {
      PlayState.instance.currentChart.song.cacheCharts(true);
    }

    super.onDestroy(event);
  }
}
